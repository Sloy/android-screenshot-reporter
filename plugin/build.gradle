apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'idea'

sourceCompatibility = 1.8

gradlePlugin {
    plugins {
        screenshotReporter {
            id = "com.sloydev.screenshot-reporter"
            implementationClass = "com.sloydev.screenshotreporter.gradle.ScreenshotReporterPlugin"
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compileOnly gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.android.tools.build:gradle:3.4.2'

    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.android.tools.ddms:ddmlib:25.3.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:0.30'
    testImplementation 'commons-io:commons-io:2.5'
    testImplementation 'com.github.geowarin:docker-junit-rule:1.1.0'
}

tasks.findByPath('test').dependsOn('jar')

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showStackTraces true
        showCauses true
        showStandardStreams true
    }
}

publish {
    userOrg = 'sloy'
    groupId = 'com.sloydev'
    artifactId = 'screenshot-reporter-plugin'
    publishVersion = lib_version
    desc = 'Take screenshot during your Espresso tests and see them later in a report.'
    website = 'https://github.com/Sloy/android-screenshot-reporter'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
